#include <WiFi.h>

// Replace with your network credentials
const char* ssid = "1111111";
const char* password = "9999";

// Web server runs on port 80
WiFiServer server(8080);

// HTML request header
String header;

// Output state
String outputState = "off";

// Use only GPIO 26
const int outputPin = 26;

unsigned long currentTime = millis();
unsigned long previousTime = 0; 
const long timeoutTime = 2000;

void setup() {
  Serial.begin(115200);
  pinMode(outputPin, OUTPUT);
  digitalWrite(outputPin, LOW);

  // Connect to Wi-Fi
  Serial.print("Connecting to ");
  Serial.println(ssid);
  WiFi.begin(ssid, password);
  
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi connected.");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
  server.begin();
}

void loop(){
  WiFiClient client = server.available();   

  if (client) {
    currentTime = millis();
    previousTime = currentTime;
    Serial.println("New Client.");
    String currentLine = "";

    while (client.connected() && currentTime - previousTime <= timeoutTime) {
      currentTime = millis();
      if (client.available()) {
        char c = client.read();
        Serial.write(c);
        header += c;
        if (c == '\n') {
          if (currentLine.length() == 0) {
            // HTTP header
            client.println("HTTP/1.1 200 OK");
            client.println("Content-type:text/html");
            client.println("Connection: close");
            client.println();

            // LED control logic
            if (header.indexOf("GET /on") >= 0) {
              Serial.println("LED ON");
              outputState = "on";
              digitalWrite(outputPin, HIGH);
            } else if (header.indexOf("GET /off") >= 0) {
              Serial.println("LED OFF");
              outputState = "off";
              digitalWrite(outputPin, LOW);
            }

            // HTML page
            client.println("<!DOCTYPE html><html>");
            client.println("<head><meta name=\"viewport\" content=\"width=device-width, initial-scale=1\">");
            client.println("<style>html { font-family: Helvetica; text-align: center;}</style></head>");
            client.println("<body><h1>ESP32 LED Control</h1>");
            client.println("<p>GPIO 26 - State: " + outputState + "</p>");

            if (outputState == "off") {
              client.println("<p><a href=\"/on\"><button>ON</button></a></p>");
            } else {
              client.println("<p><a href=\"/off\"><button>OFF</button></a></p>");
            }

            client.println("</body></html>");
            client.println();
            break;
          } else {
            currentLine = "";
          }
        } else if (c != '\r') {
          currentLine += c;
        }
      }
    }

    header = "";
    client.stop();
    Serial.println("Client disconnected.");
    Serial.println("");
  }
}
